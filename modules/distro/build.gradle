plugins {
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.0'
    // id 'com.bmuschko.docker-remote-api' version '3.1.0'
}

configurations {
    app
    distro
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    app "${group}:app-main:${vMainApp}@jar"
    app "${group}:app-standardidprovider:${vStdIdProvider}@jar"
    app "${group}:app-applications:${vApplicationsApp}@jar"
    app "${group}:app-contentstudio:${vContentStudioApp}@jar"
    app "${group}:app-users:${vUsersApp}@jar"
    distro( "${group}:runtime:${vXpRuntime}@zip" )
}

ext {
    installDir = "$buildDir/install"
    archiveBase = "enonic-xp-$version"
    appLevel = 40
}

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into installDir
}

task copyResources( type: Copy, dependsOn: unpackDistro  ) {
    destinationDir = file( installDir )
    from( 'src' )
}

task copyApps( type: Copy, dependsOn: copyResources ) {
    from( configurations.app )
    into "$installDir/system/$appLevel"
}

task copyDist( dependsOn: copyApps )

task distZip( type: Zip, dependsOn: copyDist ) {
    description = 'Build full distribution zip.'
    group = 'Dist'
    baseName = 'distro'
    from installDir
    into archiveBase
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Make sure no JARs published, if 'java' plugin is present
            // Not really necessary
            from null
            artifact distZip
        }
    }
}
